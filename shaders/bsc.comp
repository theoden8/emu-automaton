#version 430 core
#extension GL_ARB_compute_shader: enable

layout(local_size_x = 1, local_size_y = 1) in;
layout (binding = 0, r8ui) uniform uimage2D srcTex;
layout (binding = 1, r8ui) uniform uimage2D dstTex;

uniform uint bs, ss, c;
uniform int w, h, wgsize;
uniform uint access_mode;

#define BOUNDED 0
#define LOOPED 1

#define DEAD 0
#define LIVE (c - 1)

uint count_moore_neighborhood(ivec2 ind) {
  uint count = 0;
  // moore neighborhood
  for(int ix = -1; ix <= 1; ++ix) {
    for(int iy = -1; iy <= 1; ++iy) {
      if(ix == 0 && iy == 0)continue;
      int x = ind.x + ix,
          y = ind.y + iy;
      if(access_mode == BOUNDED) {
        if(y < 0 || y >= h || x < 0 || x >= w) {
          continue;
        }
      } else if(access_mode == LOOPED) {
        if(y < 0 || y >= h || x < 0 || x >= w) {
          y = (y < 0) ? y + h : y % h;
          x = (x < 0) ? x + w : x % w;
        }
      }
      uint neighbor = imageLoad(srcTex, ivec2(x, y)).r;
      count += (neighbor == LIVE) ? 1u : 0u;
    }
  }
  return count;
}

void update_state(ivec2 ind) {
  uint count = count_moore_neighborhood(ind);
  uint state = imageLoad(srcTex, ind).r;
  uint next_state = state;
  if((state == DEAD && bool(bs & (1 << count))) || (state == LIVE && bool(ss & (1 << count)))) {
    next_state = c - 1;
  } else if(next_state > 0) {
    next_state -= 1;
  }
  imageStore(dstTex, ind, uvec4(next_state));
}

void main(void) {
  ivec2 ind = ivec2(gl_GlobalInvocationID.xy);
  const int x0 = ind.x * wgsize, y0 = ind.y * wgsize;
  const int x1 = min(x0 + wgsize, w);
  const int y1 = min(y0 + wgsize, h);
  for(int x = x0; x < x1; ++x) {
    for(int y = y0; y < y1; ++y) {
      update_state(ivec2(x, y));
    }
  }
}
